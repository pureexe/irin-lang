# irin-lang
การเยื้องเพื่อการรู้จำภาษาธรรมชาติอย่างชาญฉลาด (Indent to Recognize for Intelligent Natural language : IRIN) หรือเรียกอีกชื่อว่า ภาษาไอริน โดยใช้การเยื้องของโค้ดให้เป็นประโยชน์ใน บอกรูปแบบของประโยคคำถามและคำตอบในการสร้างระบบสนทนาโต้ตอบอัตโนมัติซึ่งจะทำให้โค้ดอ่านง่ายและสั้นลงอย่างมากซึ่งเป็นมิตรต่อผู้ที่กำลังศึกษาการสร้างระบบสนทนาโต้ตอบอัตโนมัติและยังทำให้สามารถพัฒนาระบบได้อย่างรวดเร็ว อีกทั้งยังรองรับการใช้งานทั้งภาษาไทยและภาษาอังกฤษ

## การเขียนภาษาไอริน
ภาษาไอรินประกอบด้วย 2 ส่วนได้แก่ส่วนตัวและส่วนหัวโดยเราสามารถเขียนส่วนตัวโปรแกรมได้เลยโดยไม่มีส่วนหัวก็ได้ โดยส่วนตัวโปรแกรมนั้นบรรทัดที่เขียนโดยไม่มีการเยื้องเข้าไปจะเป็นคำถามและบรรทัดที่เยื้องเข้าไปจะเป็นคำตอบของคำถามหากมีเยื้องเข้าไปตามลำดับ  

ตัวอย่างการเขียนตัวโปรแกรม
```
สวัสดี
  สวัสดีค่ะ
```
เมื่อผู้ใช้พิมพ์คำว่า "สวัสดี" ระบบจะตอบว่า "สวัสดีค่ะ" แต่ถ้าหากผู้ใช้พิมพ์อย่างอื่นระบบจะตอบกลับมาว่า Error เนื่องจากตัวแปลภาษาไม่สามารถหารูปแบบที่เหมือนกันกับค่าที่รับค่าเข้ามา ดังนั้นเราจึงจำเป็นต้องให้โปรแกรมมีคำสั่งสำหรับตอบกลับเสมอเพื่อป้องกันข้อผิดพลาดที่จะเกิดขึ้น โดยเราสามารถใช้นิพจน์ช่วยในการจดจำรูปแบบได้  

####นิพจน์
คือ รูปแบบสำหรับเปรียบเทียบข้อความว่าเข้าเงื่อนไขหรือไม่ หานิพจน์ตรงกับรูปแบบของศัพท์ที่กรอกเข้ามาจะทำการเรียกคำตอบของคำถามนั้นถัดไป ตัวอย่างเช่น  

1. `*` คือตรงกับทุกตัวอักษร ไม่ว่าตัวอักษรใดก็ตามจะเป็นจริงเสมอ ตัวอย่างเช่น  
  ```
  สวัสดี
    สวัสดีค่ะ
  *
    ขออภัยค่ะ ดิฉันไม่เข้าใจ
  ```
  หากผู้ใช้กรอกว่า อะไร ระบบจะตอบกลับว่า ขออภัยค่ะ ดิฉันไม่เข้าใจ เนื่องจาก * หมายถึงตรงกับทุกตัวอักษร ดังนั้น คำว่า อะไร จึงมีค่าเป็นจริงเมื่อเจอกับนิพจน์ * ทำให้ตัวแปลภาษาดึงคำตอบของ * มาตอบ  

2. `()` คือ **เลือก** คำใดคำหนึ่งจากในวงเล็บ แบ่งคำออกจากกันด้วยเครื่องหมาย `|` ตัวอย่างเช่น
  ```
  หิว(ไหม|หรือไม่)
    หิวแล้วค่ะ
  ```
  หากผู้ใช้กรอกว่า หิวไหม หรือ หิวหรือไม่ จะได้คำตอบคือ หิวแล้วค่ะ แต่หากผู้ใช้กรอกว่า หิวไหมหรือไม่ ตัวแปลภาษาจะไม่สามารถหาคำมาตอบได้เนื่องจากวงเล็บแทนให้เลือกคำใดคำหนึ่งเท่านั้น  

3. `[]` คือ **อาจ** มีคำใดคำหนึ่งจากในวงเล็บ แบ่งคำออกจากกันด้วยเครื่องหมาย `|` ตัวอย่างเช่น  
  ```
  สวัสดี[ครับ|ค่ะ]
    สวัสดีค่ะ
  ```
  หากผู้ใช้กรอกคำว่า สวัสดี สวัสดีครับ หรือ สวัสดีค่ะ ตัวแปลภาาษาจะตอบกลับมาว่า สวัสดีค่ะ แต่หากกรอกคำว่า สวัสดีนะ ตัวแปลภาษาจะไม่สามารถหาคำตอบให้ได้ เนื่องจากเครื่องหมายนี้แทนว่าอาจมีหรือไม่มีคำนั้นอยู่  

####จากบนลงล่าง
ตัวแปลภาษาจะอ่านข้อมูลจากบนลงร่างเสมอ สำหรับคำถามระบบจะเลือกคำถามที่มีรูปแบบตรงกับประโยคที่รับเข้าประโยคแรกมาตอบ แต่คำตอบนั้นจะสุ่มจากคำตอบของคำถามกรณีมีคำตอบมากกว่า 1 คำตอบ ตัวอย่างเช่น  
```
สวัสดี
  สวัสดีค่ะ
  ดีคร้า
  หวัดดีนะ
*
  ขออภัยไม่เข้าใจคำถาม
```
เมื่อผู้ใช้กรอกคำว่า สวัสดี เข้ามา ถึงแม้ว่าคำว่าสวัสดีจะตรงเงื่อนไขทั้ง สวัสดี และ * แต่เนื่องจากเงื่อนไข สวัสดี อยู่บรรทัดบน * ดังนั้นตัวแปลภาษาจึงเลือกเงื่อนไข สวัสดี แต่เงื่อนไขสวัสดีนั้นมี 3 คำตอบดังนั้นตัวแปลภาษาจึงสุ่มคำตอบจาก สวัสดีค่ะ ดีคร้า และหวัดดีนะ  

###ความต่อเนื่อง
ในภาษาไอรินเราสามารถใช้การสนทนาต่อเนื่องได้โดยการเพิ่มจำนวนครั้งการเยื้องเข้าไปได้เช่น
```
สวัสดี
  สวัสดีค่ะ
นอนละ
  ฝันดีค่ะ
หิวข้าว
  ไปกินสิค่ะ
    ไม่มีเงิน
      เดี๋ยวให้ยืมค่ะ
        ไม่คืนได้ไหม
          ไม่ได้ค่ะ
    ไม่หิวแล้ว
      แล้วจะบอกว่าหิวข้าวทำไมค่ะ
*
  ขออภัยไม่เข้าใจคำถาม
```
จะเห็นว่าบริเวณคำว่าหิวข้าวมีการเยื้องเข้าไป หมายถึงการสนทนาต่อเนื่อง เมื่อเรากรอกคำว่า  สวัสดี ตัวแปลภาษาจะตอบว่า สวัสดีค่ะ แล้วเรากรอกต่อด้วยคำว่า หิวข้าว ระบบจะตอบกลับมาว่า ไปกินสิค่ะ ซึ่งช่วงนี้จะเป็นการสนทนาต่อเนื่องซึ่งตอนนี้มี 2 ทางเลือกคือ ไม่มีเงิน กับไม่หิวแล้ว ซึ่งหากเรากรอกว่าไม่มีเงิน ตัวแปลภาษาจะตอบว่า เดี๋ยวให้ยืมค่ะ แต่หากเรากรอกว่านอนละ ตัวแปลภาษาจะออกจากการสนทนาต่อเนื่องแล้วตอบว่า ฝันดีค่ะ

####หัวข้อเรื่อง
ในภาษาไอริน ฟังก์ชันนั้นจะใช้เครื่องหมาย `->` แทนการกำหนดหัวข้อเรื่อง ทั้งการประกาศหัวข้อเรื่องและการเรียกใช้หัวข้อเรื่อง โดยการประกาศและใช้หัวข้อเรื่องจะ `-> ชื่อหัวข้อ` ต่างกันที่การประกาศฟังก์ชันนั้นก่อนหน้าเครื่องหมาย `->` จะไม่มีการเยื้องเลย หากมีการเยื้องจะเป็นการนำพาเข้าสู่หัวข้อเรื่องนั้น ตัวอย่างเช่น  
```
สวัสดี
  สวัสดีค่ะ
    ->จากลา
->จากลา
  ไปละ
    ลาก่อนค่ะ
```
เมื่อผู้ใช้กรอกคำว่าสวัสดีตัวแปลภาษาจะตอบว่าสวัสดีค่ะ และเมื่อผู้ใช้กรอกคำว่า ไปละ ต่อระบบจะตอบว่า ลาก่อนค่ะ เนื่องจากเยื้องของคำว่าสวัสดีค่ะ มีการเรียกใช้หัวข้อจากลา ซึ่งจะเป็นการดึงเอาคำถามและคำตอบที่อยู่ในหัวข้อจากลาไปเชื่อมอยู่กับคำถามสวัสดี ดังนั้นคำถามที่อยู่ต่อจากคำว่าสวัสดีค่ะก็คือคำว่าไปละ

ซึ่งการเรียกใช้หัวข้อเรื่องสามารถเรียกหัวข้อจากไฟล์อื่นได้โดยการนำหัวข้อเรื่องใส่ไว้ในไฟล์อื่นแล้วตั้วชื่อไฟล์ด้วยนามสกุล `.irin` แล้วทำการเรียกใช้ด้วยคำสั่งฟังก์ชัน

ตัวอย่างเช่น ไฟล์แรกเขียนว่า

```
สวัสดี
  สวัสดีค่ะ
    ->goodbye.irin
```

แล้วในไฟล์ goodbye.irin ให้ทำการเขียนประโยคการโต้ตอบได้เลยโดยไม่ต้องใส่ชื่อหัวข้อเรื่อง

```
ไปละ
  ลาก่อนค่ะ
```

จะเห็นได้ว่าเมื่อรันแล้วผลลัพธ์จะสามารถใช้งานได้เหมือนกับการเขียนหัวข้อเรื่องไว้ในไฟล์เดียวกัน

**หมายเหตุ:** หัวข้อเรื่องเดิมใช้ชื่อว่า **ฟังก์ชัน** แต่เนื่องจากไม่สามารถส่งผ่านค่าพารามิเตอร์ได้เหมือนฟังก์ชันปกติจึงขอเรียกว่าหัวข้อเรื่องแทนครับ

####ส่วนหัว
ส่วนหัวมีไว้สำหรับการประกาศตัวแปรและนำเข้าไฟล์โค้ดโดยส่วนหัวจะเริ่มต้นด้วยขีดกลาง(Hyphen) 3 ขีด และปิดด้วยขีดกลางอีก 3 ขีด
โดยส่วนหัวใช้เพื่อประกาศตัวแปรและเรียกหัวข้อเรื่องจากไฟล์อื่นเข้ามาใช้งาน
```
---
-> brain/main.irin
-> brain/everyday.irin
name <- ไอริน
gender <- หญิง
---
```
จากคำสังข้างต้นคือการเรียกใช้หัวข้อเรื่องจากไฟล์ main.irin และ everyday.irin อีกทั้งยังประกาศตัวแปรโดยใช้เครื่องหมาย `<-` โดยกำหนดตัวแปรชื่อ name เก็บค่าว่าไอริน และตัวแปรชื่อ gender เก็บค่าว่าหญิง

#### การเรียกตัวแปร
การเรียกค่าในตัวแปรสามารถทำได้โดยการใช้เครื่องหมาย `{}`
```
---
name <- ไอริน
---
ผมอยาก*
  {name}จะช่วยคุณ{1}เองค่ะ
```
ถ้ากรอกว่า ผมอยากสร้างบ้าน ตัวแปรภาษาจะตอบว่า ไอรินจะช่วยคุณสร้างบ้างเองค่ะ โดย `{ชื่อตัวแปร}`หมายถึงการเรียกค่าในตัวแปรมาใช้งาน โดยค่าใน {name} คือไอริน และ `{ตัวเลข}` คือการนำค่าที่ได้มาจากตำแหน่งของ `()` และ `*` มาใช้งานโดยตำแหน่งแรกคือเลข `{1}` ซึ่งในตำแหน่งของ * จะเห็นว่าคือคำว่าสร้างบ้าน ทั้งนี้ `[]` จะไม่ถูกเก็บค่าใส่ในตัวแปรเนื่องจากผมลัพธ์ที่ได้อาจจะมีค่าหรือไม่มีค่าก็ได้

#### กำหนดค่าตัวแปรแบบพลวัต
เราสามารถเปลี่ยนแปลงค่าของตัวแปรที่มีอยู่เดิมได้การกำหนดค่าตัวแปรเข้าไปใหม่แบบพลวัตโดยทำการใช้เครื่องหมาย `<-` เพื่อกำหนดค่าตัวแปรเข้ามาใหม่ภายใต้เครื่องหมายปีกกา

```
---
user <- คุณ
---
สวัสดี
  สวัสดีค่ะ คุณชื่ออะไรหรอคะ
    [ผม|ฉัน][ชื่อ]*[ครับ|ค่ะ]
      สวัสดีค่ะคุณ{1}{user<-{1}}
หิวข้าวจัง
  ทำไม{user}ไม่ไปกินข้าวละคะ?
```
เมื่อพิมพ์คำว่า หิวข้าวจัง ระบบจะตอบว่า ทำไมคุณไม่ไปกินข้าวละคะ? แต่เมื่อพิมพ์ว่า สวัสดี ระบบตอบกลับมาว่า สวัสดีค่ะ คุณชื่ออะไรหรอคะ เมื่อพิมพ์ไปว่า ผมชื่อเพียวครับ ระบบจะเปลี่ยนค่าตัวแปร user จากคุณเป็นเพียว พร้อมตอบกับมาว่า สวัสดีค่ะคุณเพียว และเมื่อพิมพ์ว่าหิวข้าวจังอีกรอบ ระบบจะตอบว่า ทำไมเพียวไม่ไปกินข้าวละคะ?

#### การใช้งานเงื่อนไขแบบในบรรทัด
เราสามารถทำให้คำตอบการสนทนาเปลี่ยนไปได้โดยการใช้เงื่อนไขในบรรทัดได้ โดยใช้เครื่องหมาย `?` ตามด้วยคำตอบเมื่อเป็นจริง แบ่งด้วย `:` แล้วตามด้วยเงื่อนไขเมื่อเป็นเท็จ

โดยเครื่องหมายในการเปรียบเทียบที่สามารถใช้ได้ ได้แก่ และ (`&&`), หรือ (`||`), นิเศษ (`!`), เท่ากับ (`==`), มากกว่าเท่ากับ (`>=`), น้อยกว่าเท่ากับ (`<=`), มากกว่า (`>`), น้อยกว่า (`<`), บวก(`+`), ลบ (`-`), คูณ (`*`) และหาร (`/`)
```
เธออายุเท่าไร
  อายุ 18 ค่ะ แล้วคุณอายุของคุณเท่าไรคะ?
    [ผม|ฉัน][อายุ]*[ครับ|ค่ะ]
      คุณนี่ดู{{1}<18?เด็ก:แก่}จังเลยค่ะ
```
เมื่อถามระบบว่า เธออายุเท่าไร ระบบจะตอบว่า อายุ 18 ค่ะ แล้วคุณอายุของคุณเท่าไรคะ? เมื่อตอบกลับไปว่า อายุ 20 ครับ ระบบจะตอบกลับมาว่า คุณนี่ดูแก่จังเลยค่ะ แต่หากตอบกลับไปว่า อายุ 12 ครับ คุณนี่ดูเด็กจังเลยค่ะ


## พัฒนาโดย
นายภัคพล พงษ์ทวี  
ติดต่อ : phongthawee_p@silpakorn.edu  
Personal Website: [www.pureapp.in.th](https://www.pureapp.in.th)  
Project Website: [lang.irin.in.th](https://lang.irin.in.th)
